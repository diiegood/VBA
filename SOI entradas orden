Sub SOI_USD()

    Dim wsOrigen As Worksheet
    Dim wsDestino As Worksheet
    Dim ultimaFila As Long
    Dim i As Long, filaDestino As Long
    Dim SSItext As String
    Dim tipoOp As String
    Dim contraparte As String
    Dim filasSalidas As Collection
    Dim filasEntradas As Collection
    Dim idx As Variant

'##################################################################################################
    
    'Asignación de hojas
    Set wsOrigen = ThisWorkbook.Sheets("Reporte Garantias") ' Hoja origen
    Set wsDestino = ActiveSheet                              ' Hoja destino
    
    'Última fila con datos
    ultimaFila = wsOrigen.Cells(wsOrigen.Rows.Count, "A").End(xlUp).Row
    
    'Limpiar rango destino
    With wsDestino.Range("A21:D29")
        .ClearContents
        .Borders.LineStyle = xlNone
    End With
    
    filaDestino = 21
    
    ' Crear colecciones para separar salidas y entradas
    Set filasSalidas = New Collection
    Set filasEntradas = New Collection
    
    ' --- Identificar filas ---
    For i = 2 To ultimaFila
        If Trim(UCase(wsOrigen.Cells(i, "H").Value)) = "USD" Then
            tipoOp = UCase(Trim(wsOrigen.Cells(i, "D").Value))
            ' Saltar los que contienen "SSISUB"
            If InStr(1, UCase(wsOrigen.Cells(i, "C").Value), "SSISUB") = 0 Then
                If tipoOp = "SALIDA" Then
                    filasSalidas.Add i
                ElseIf tipoOp = "ENTRADA" Then
                    filasEntradas.Add i
                End If
            End If
        End If
    Next i

'##################################################################################################
' --- Procesar SALIDAS ---
    For Each idx In filasSalidas
        LlenarLinea wsOrigen, wsDestino, CLng(idx), filaDestino, "SALIDA"
        filaDestino = filaDestino + 1
        If filaDestino > 29 Then Exit For
    Next idx

' --- Procesar ENTRADAS ---
    For Each idx In filasEntradas
        LlenarLinea wsOrigen, wsDestino, CLng(idx), filaDestino, "ENTRADA"
        filaDestino = filaDestino + 1
        If filaDestino > 29 Then Exit For
    Next idx

'##################################################################################################
' --- Formatos finales ---
    With wsDestino.Range("A21:D29")
         .Font.Name = "Arial"
         .Font.Size = 10
         .VerticalAlignment = xlCenter
    End With
     
    With wsDestino.Range("B21:C29")
         .NumberFormat = "_($* #,##0.00_);_($* (#,##0.00);_($* ""-""??_);_(@_)"
         .HorizontalAlignment = xlCenter
    End With
     
    wsDestino.Columns("D").AutoFit
    wsDestino.Rows("21:29").AutoFit
        
    MsgBox "Proceso completado SOI USD", vbInformation

End Sub

'###############################################################################################
' SUB auxiliar: llena una línea en el destino según el tipo de operación
'###############################################################################################

Private Sub LlenarLinea(wsOrigen As Worksheet, wsDestino As Worksheet, i As Long, filaDestino As Long, tipo As String)

    Dim SSItext As String
    Dim contraparte As String
    
    ' --- Obtener contraparte y reemplazar código por nombre ---
    contraparte = wsOrigen.Cells(i, "C").Value
    Select Case UCase(Trim(contraparte))
        Case "BBVI"
            contraparte = "BBVA BANCOMER, S.A."
        Case "CHMX"
            contraparte = "BANCO J.P. MORGAN S.A."
        Case "CITIMEX"
            contraparte = "BANCO CITI MÉXICO S.A. INSTITUCIÓN DE BANCA MÚLTIPLE, GRUPO FINANCIERO CITI MÉXICO"
        Case "BSAN"
            contraparte = "BANCO SANTANDER, S.A. INSTITUCION DE BANCA MULTIPLE GRUPO FINANCIERO SANTANDER"
        Case "CBGSMX"
            contraparte = "GOLDMAN SACHS MEXICO CASA DE BOLSA"
        Case "BNP PARIS"
            contraparte = "BNP PARIBAS"
        Case "BBMX"
            contraparte = "BARCLAYS BANK MEXICO, S.A., INSTITUCION DE BANCA MULTIPLE, GRUPO FINANCIERO BARCLAYS MEXICO."
        Case "RBMX"
            contraparte = "HSBC MEXICO, S.A. INSTITUCION DE BANCA MULTIPLE"
    End Select
    
    ' --- Armar texto SSI solo si es SALIDA ---
    SSItext = ""
    If tipo = "SALIDA" Then
        If wsOrigen.Cells(i, "M").Value <> "" Then SSItext = SSItext & "BIC Corresponsal: " & wsOrigen.Cells(i, "M").Value & vbNewLine
        If wsOrigen.Cells(i, "N").Value <> "" Then SSItext = SSItext & "Banco Corresponsal: " & wsOrigen.Cells(i, "N").Value & vbNewLine
        If wsOrigen.Cells(i, "P").Value <> "" Then SSItext = SSItext & "BIC Beneficiario: " & wsOrigen.Cells(i, "P").Value & vbNewLine
        If wsOrigen.Cells(i, "Q").Value <> "" Then SSItext = SSItext & "Cuenta Beneficiario: " & wsOrigen.Cells(i, "Q").Value
    End If

    ' --- Colocar datos según tipo ---
    wsDestino.Cells(filaDestino, "A").Value = contraparte
    wsDestino.Cells(filaDestino, "A").WrapText = False
    
    If tipo = "SALIDA" Then
        wsDestino.Cells(filaDestino, "C").Value = wsOrigen.Cells(i, "F").Value ' Importe en columna C
    ElseIf tipo = "ENTRADA" Then
        wsDestino.Cells(filaDestino, "B").Value = wsOrigen.Cells(i, "F").Value ' Importe en columna B
    End If
    
    wsDestino.Cells(filaDestino, "D").Value = SSItext
    wsDestino.Cells(filaDestino, "D").WrapText = True

End Sub

